Q = {1,2,3,4,5,6,7,8,9,10};
precise int [__NUMNODES__] DistGlobal;

0:[
  dynamic int [__NUMNODES__] distance;
  dynamic int [__SLICESIZE__] slice;  
  dynamic int newDist;

  precise int mystart;
  precise int myend;
  precise int i;
  precise int j;
  precise int lastthread;
  precise int mysize;

  distance = DistGlobal;  

  i = 0;
  for q in Q do {
    mystart = i*NodesPerThread;
	  myend = (i+1)*NodesPerThread;
    lastthread = (i == Num_threads - 1);
    if lastthread then {
       myend = Num_nodes;
    };
    send(q, precise int, mystart);
    send(q, precise int, myend);

    i = i + 1;
  };
  
  repeat 5 {
    for q in Q do {
      send(q, precise int[], distance);
    };
    i = 0;
    for q in Q do {
        mystart = i*NodesPerThread;
        myend = (i+1)*NodesPerThread;
        lastthread = (i == Num_threads - 1);
        if lastthread then {
           myend = Num_nodes;
        };

        slice = receive(q, dynamic int[]);        

        mysize = myend - mystart;
        j = 0;
        repeat mysize {
          newDist = slice[j];
          distance[mystart + j] = newDist;
          j = j + 1;
        };
        i = i + 1;
    };
    ## diesel.PrintWorstElement(DynMap[:], 0, __NUMNODES__) ##;
  };

  DistGlobal = distance;
]

||

q in Q:[
  precise int [__NUMNODES__] inlinks;
  dynamic int [__NUMNODES__] distances;
  dynamic int distance;
  dynamic int [__SLICESIZE__] newDistance;
  dynamic int condition;

  precise int inlink;
  precise int neighbor;
  
  precise int nodeInlinks;
  precise int i;
  precise int mystart;
  precise int myend;
  dynamic int cur;
  dynamic int temp;
  dynamic int temp1;
  precise int mysize;
  
  temp1 = 1;
  inlinks = Inlinks;
  mystart = receive(0, precise int);
  myend = receive(0, precise int);

  repeat 5 {
    distances = receive(0, precise int[]);
    mysize = myend - mystart;
    i = 0;
    repeat mysize {
      cur = mystart + i;
      nodeInlinks = inlinks[cur];
      distance = 0;
      inlink = 0;
      repeat nodeInlinks {
        neighbor = Edges[cur*__MAX_DEGREE_ + inlink];
        temp = distances[neighbor];
        condition = (temp == 1);
        distance = condition ? temp1 distance;
        inlink = inlink + 1;
      };
      newDistance[i] = distance;
      i = i +1;
    };

    send(0, dynamic int[], newDistance);
  };
]