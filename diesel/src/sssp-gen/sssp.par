Q = {1,2,3,4,5,6,7,8,9,10};
precise int [62586] DistGlobal;

0:[
  dynamic int [62586] distance;
  dynamic int newDist;

  precise int mystart;
  precise int myend;
  precise int i;
  precise int j;
  precise int lastthread;
  precise int mysize;

  distance = DistGlobal;  

  i = 0;
  for q in Q do {
    mystart = i*NodesPerThread;
	  myend = (i+1)*NodesPerThread;
    lastthread = (i == Num_threads);
    if lastthread then {
       myend = Num_nodes;
    };

    send(q, precise int[], Edges);
    send(q, precise int[], Inlinks);
    send(q, precise int[], Outlinks);
    send(q, precise int, mystart);
    send(q, precise int, myend);

    i = i + 1;
  };
  
  repeat 10 {
    for q in Q do {
      send(q, precise int[], distance);
    };
    i = 0;
    for q in Q do {
        mystart = i*NodesPerThread;
        myend = (i+1)*NodesPerThread;
        lastthread = (i == Num_threads);
        if lastthread then {
           myend = Num_nodes;
        };

        mysize = myend - mystart;
        j = 0;
        repeat mysize {
          newDist = receive(q, precise int);
          distance[mystart + j] = newDist;
          j = j + 1;
        };
        i = i + 1;
    };  
  };

  DistGlobal = distance;
]

||

q in Q:[
  precise int [62586000] edges;
  precise int [Num_nodes] inlinks;
  precise int [Num_nodes] outlinks;
  dynamic int [62586] distances;
  dynamic int distance;
  dynamic int [10000] newDistance;
  dynamic int condition;

  precise int inlink;
  precise int neighbor;
  
  precise int nodeInlinks;
  precise int i;
  precise int mystart;
  precise int myend;
  dynamic int cur;
  dynamic int temp;
  dynamic int temp0;
  precise int mysize;

  edges = receive(0, precise int[]);
  inlinks = receive(0, precise int[]);
  outlinks = receive(0, precise int[]);  
  mystart = receive(0, precise int);
  myend = receive(0, precise int);

  repeat 10 {
    distances = receive(0, precise int[]);
    mysize = myend - mystart;
    i = 0;
    repeat mysize {
      cur = mystart + i;
      nodeInlinks = inlinks[cur];
      distance = distances[cur];
      inlink = 0;
      repeat nodeInlinks {
        neighbor = edges[cur*1000 + inlink];
        temp = distances[neighbor];
        temp0 = temp + 1;
        condition = (distance > temp0);        
        distance = condition ? temp0 distance;
        inlink = inlink + 1;
      };
      newDistance[i] = distance;
      i = i +1;
    };

    i = 0;
    repeat mysize {
      temp0 = newDistance[i];
      temp = temp0 [0.9999] -1;
      send(0, precise int, temp);
      i = i + 1;
    };
  };
]